plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.openapi.generator' version '7.16.0'
    id 'org.flywaydb.flyway' version '9.22.0'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

// centralize versions used in this build
ext {
    postgresqlVersion = '42.6.0'
    flywayVersion = '9.21.2'
}

tasks.openApiGenerate {
    doFirst {
        def specFile = file(inputSpec)
        if (!specFile.exists()) {
            throw new GradleException("OpenAPI spec file not found: $specFile")
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
    implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    implementation 'io.springfox:springfox-oas:3.0.0'
    implementation platform('org.springframework.boot:spring-boot-dependencies:3.5.6')
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.15'

    // JPA + PostgreSQL driver (runtime)
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly "org.postgresql:postgresql:${postgresqlVersion}"

    // Flyway runtime libraries
    implementation "org.flywaydb:flyway-core:${flywayVersion}"
//    implementation "org.flywaydb:flyway-database-postgresql:${flywayVersion}"

    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    compileOnly 'org.projectlombok:lombok:1.18.30'
    compileOnly 'org.springframework.boot:spring-boot-starter-validation'
    compileOnly 'org.openapitools:jackson-databind-nullable:0.2.3'

    testImplementation 'org.testcontainers:testcontainers:1.19.7'
    testImplementation 'org.testcontainers:postgresql:1.19.7'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/src/main/resources/api/openapi.yaml"
    outputDir = "$buildDir/generated-sources/openapi"
    apiPackage = "com.example.api"
    modelPackage = "com.example.model"
    configOptions = [
            useJakartaEe: "true",
            dateLibrary: "java8",
            interfaceOnly: "true", // This tells the generator to emit only interfaces
            skipDefaultInterface: "true", // Optional: avoids generating a default stub
            useTags: "true" // Split files by tag
    ]
    ignoreFileOverride = "$rootDir/src/main/resources/api/.openapi-generator-ignore"
}

tasks.compileJava {
    dependsOn tasks.openApiGenerate
}

afterEvaluate {
    sourceSets.main.java.srcDirs += file("$buildDir/generated-sources/openapi/src/main/java")
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += [
            "-Xlint:unchecked",
            "-Xlint:deprecation"
    ]
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integrationTest/java')
        resources.srcDir file('src/integrationTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter("test")
    useJUnitPlatform()
}

tasks.named('check') {
    dependsOn(tasks.named('integrationTest'))
}

springBoot {
    mainClass = 'com.example.ecommercedemo.EcommerceDemoApplication'
}

// Flyway configuration (replaces the Maven plugin configuration)
flyway {
    url = 'jdbc:postgresql://localhost:5432/ecomm'
    user = 'packt'
    password = 'packt'
    // optionally set locations etc:
    // locations = ['filesystem:src/main/resources/db/migration']
}